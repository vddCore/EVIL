fn test_invocation(f) 
  -> f();

fn generate_closure_with_parameter_value(x)
    -> (fn(a) -> a + x);

#[test(12)]
fn basic_closure_parameter_hides_local() {
    var a = 10;
    
    var f = fn(a) {
        ret a;
    };
    
    ret f(12);
}

#[test(12)]
fn basic_closure_captures_local() {
    var b = 10;
    
    var f = fn() {
        ret b + 2;
    };
    
    ret test_invocation(f);
}

#[test(12)]
fn basic_closure_captures_parameter() {
    var f = generate_closure_with_parameter_value(10);
    ret f(2);
}

#[test(21.37);approximate(2)]
fn nested_closure_captures_outside_local() {
    var a = 21;
    var f = (fn() -> (fn() -> a + .37));
    
    ret f()();
}

#[test(21.37);approximate(2)]
fn nested_closure_captures_closure_local() {
    var a = 111;
    
    var f = fn() {
        var a = 21;
        ret fn() { ret a + .37; };
    };
    
    ret f()();
}

#[test(21.37);approximate(2)]
fn nested_closure_captures_closure_parameter() {
    var a = 111;
    var f = fn(a) {
        ret fn() { ret a + .37; };
    };
    
    ret f(21)();
}

#[test(21.37);approximate(2)]
fn extreme_closure_nesting() {
    var a = 10;
    ret test_invocation(fn(b) {
        var f = fn(c) {
            ret fn() { ret a + b + c; };
        };
        
        ret f(2);
    }(9)) + .37;
}


fn filter_table(t, predicate) {
    var result = { };
    
    each (var k, v : t) {
        if (predicate(k, v)) {    
            result[k] = v;
        }
    }
    
    ret result;
}

#[test]
fn predicate_anonymous_function() {
    var t = { 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14 };
    var t2 = filter_table(
        t, 
        fn(k, v) -> v % 2 == 0
    ); 
    
    ret #t2 == #t / 2; 
}

fn delay(ms) { 
  var end = time.stamp.ms + ms;
  while(time.stamp.ms < end){}
  
  ret 2.137;
}

fn spawn_yielding_closure() {
  ret fn() {
    var start = time.stamp.ms;
    var retval = yield<delay>(500);
    
    ret { 
      stamp_result: time.stamp.ms,
      retval: retval
    };
  };
}

#[test]
fn yield_delay_from_closure() {
  var f = spawn_yielding_closure();
  var r = yield<f>();
  
  ret r.stamp_result >= 1000 
   && r.retval == 2.137;
}

fn spawn_closure_for_loop(a) {
  ret fn(b) -> a + b;
}

fn all(t, predicate) {
  rw var result = true;
  
  each (var k, v : t) {
    result &= predicate(k, v);
  }
  
  ret result;
}

#[test]
fn spawn_closures_in_loop() {
  var results = { };
  for (rw var i = 0; i < 11; i++) {
    results[i] = spawn_closure_for_loop(i)(1);
  }
  
  ret all(results, fn(k,v) -> v == k + 1);
}

#[test(59)]
fn many_subchunks_capture_same_local_no_change() {
  var local_a = 10;
  
  var f1 = fn(x) -> local_a + x;
  var f2 = fn(x) -> local_a * x;
  var f3 = fn(x) -> local_a - x;
  var f4 = fn(x) -> local_a / x;
  
  ret (f1(2) + f2(4) + f3(5) + f4(5));
}

#[test(59)]
fn many_subchunks_capture_same_local_after_change() {
  rw var local_a = 11;
  var f1 = fn(x) -> local_a + x;
  
  local_a = 10;
  var f2 = fn(x) -> local_a * x;
  
  local_a = 9;
  var f3 = fn(x) -> local_a - x;
  
  local_a = 8;
  var f4 = fn(x) -> local_a / x;
  
  ret f1(2) + f2(4) + f3(5) + f4(4);
}

fn invoke_func_out_of_scope(f) -> f();

#[test]
fn after_invocation_var_return() {
  var f = fn() -> 21.37;
  var res = f();
  var res2 = invoke_func_out_of_scope(f);
  
  ret res == res2;
}

fn make_counter() {
  rw var counter = 0;
  
  ret fn() {
    counter++;
    ret counter;
  };
}

#[test(10)]
fn internal_closure_state_is_preserved() {
  var counter = make_counter();
  rw var result = 0;
  
  for (rw var i = 0; i < 10; i++) {
    result = counter();
  }
    
  ret result;
}

#[test]
fn parameter_is_preserved() {
  var start_at = fn (x) {
    ret fn(y) -> x + y;
  };
  
  var f1 = start_at(1);
  var f2 = start_at(5);
  var f3 = start_at(9);
  
  ret f1(3) == 4 && f2(3) == 8 && f3(3) == 12;
}